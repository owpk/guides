plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

def authServer = project(":authorization-server")
def resServer = project(":resource-server")
def restClient = project(":rest-client")

repositories {
    mavenCentral()
}

configure([authServer, resServer, restClient]) {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'org.owpk'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    test {
        useJUnitPlatform()
    }
}

configure authServer, {
    dependencies {
        implementation 'org.springframework.security:spring-security-oauth2-authorization-server:0.2.0'
    }
}

configure resServer, {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    }
}

configure restClient, {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        testImplementation 'io.projectreactor:reactor-test'
    }
}

tasks.getByName "bootJar", {
    enabled = false
}
